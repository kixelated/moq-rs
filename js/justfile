#!/usr/bin/env just --justfile

# Using Just: https://github.com/casey/just?tab=readme-ov-file#installation

# List all of the available commands.
default:
  just --list

# Run the web server
web:
	pnpm -r i
	pnpm -r run dev

# Run the CI checks
check flags="":
	pnpm -r install {{flags}}

	# Make sure Typescript compiles
	pnpm -r run check

	# Format/lint the JS packages
	pnpm -r exec biome check

	# Make sure the JS packages are not vulnerable
	pnpm -r exec pnpm audit

	# TODO: Check for unused imports (fix the false positives)
	# pnpm exec knip --no-exit-code

# Automatically fix some issues.
fix flags="":
	# Fix the JS packages
	pnpm -r install {{flags}}

	# Format and lint
	pnpm -r exec biome check --fix

	# Make sure the JS packages are not vulnerable
	pnpm -r exec pnpm audit --fix

# Run any CI tests
test flags="":
	pnpm -r install {{flags}}

	# Run the JS tests via node.
	pnpm -r test

# Upgrade any tooling
upgrade:
	# Update the NPM dependencies
	pnpm self-update
	pnpm -r update
	pnpm -r outdated

# Build the packages
build flags="":
	pnpm -r install {{flags}}
	pnpm -r run build

# Generate documentation
doc: build
	# Currently bugged for hang
	pnpm --dir moq exec api-extractor run --local --verbose
	pnpm exec api-documenter markdown --input api --output doc
